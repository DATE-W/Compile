PROG -> HEADER SUBPROG
HEADER -> PROGRAM ID

SUBPROG -> CONST_STATEMENT VARIABLE_STATEMENT STATEMENT

CONST_STATEMENT -> CONST_ ; | ^
CONST_ -> CONST CONST_DEF | CONST_ , CONST_DEF
CONST_DEF -> ID = UINT
UINT -> num

VARIABLE_STATEMENT -> VARIABLE_ ; | ^
VARIABLE_ -> VAR ID | VARIABLE_ , ID
ID -> id

COMP_STATEMENT -> COMP_BEGIN END
COMP_BEGIN -> BEGIN STATEMENT | COMP_BEGIN ; STATEMENT

STATEMENT -> ASSIGN_STATEMENT | COND_STATEMENT | WHILE_STATEMENT | COMP_STATEMENT | ^

ASSIGN_STATEMENT -> ID := EXPR
EXPR -> PLUS ITEM | ITEM | EXPR PLUS ITEM
ITEM -> FACTOR | ITEM MUL FACTOR
FACTOR -> ID | UINT | ( EXPR )

PLUS -> + | -
MUL -> * | /
REL -> = | < | <= | > | >= | <>

COND_STATEMENT -> IF CONDITION THEN M_IF STATEMENT
M_IF -> ^
WHILE_STATEMENT -> WHILE M_BEFORE_WHILE CONDITION DO M_AFTER_WHILE STATEMENT
M_BEFORE_WHILE -> ^
M_AFTER_WHILE -> ^
CONDITION -> EXPR REL EXPR
